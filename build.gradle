plugins {
    id 'java-library'
    id 'application'
    // https://github.com/johnrengelman/shadow/releases
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    // https://github.com/nebula-plugins/gradle-lint-plugin/releases/tag/v19.0.2
    id 'nebula.lint' version "19.0.2"
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
 }

dependencies {
    def mapsforgeVersion = '+'
    // https://mvnrepository.com/artifact/org.eclipse.jetty.http2/http2-server
    def jettyVersion = '11.0.21'
    def slf4j = '+'

    implementation "org.mapsforge:mapsforge-core:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-reader:${mapsforgeVersion}"
    runtimeOnly "org.mapsforge:mapsforge-themes:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-awt:${mapsforgeVersion}"

    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    runtimeOnly "org.eclipse.jetty.http2:http2-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-util:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-xml:${jettyVersion}"
    implementation "org.eclipse.jetty.toolchain:jetty-jakarta-servlet-api:+"

    runtimeOnly "org.slf4j:slf4j-reload4j:${slf4j}"
    implementation "org.slf4j:jul-to-slf4j:${slf4j}"
    implementation "org.slf4j:slf4j-api:${slf4j}"
    implementation "commons-cli:commons-cli:+"
    implementation "org.apache.commons:commons-lang3:+"
    runtimeOnly "org.xerial:sqlite-jdbc:+"
    runtimeOnly "net.sf.kxml:kxml2:+" 

    implementation "com.github.marschall:memoryfilesystem:+"
}

apply plugin : "java"

ext {
   javaMainClass = "com.telemaxx.mapsforgesrv.MapsforgeSrv"
}

application {
     mainClass.set(javaMainClass)
}

// https://github.com/johnrengelman/shadow
shadowJar {
   archiveBaseName.set('mapsforgesrv-fatjar')
   // https://github.com/johnrengelman/shadow/issues/341#issuecomment-445556737
   mergeServiceFiles {
        path = 'META-INF/services'
        include 'org.eclipse.jetty.http.HttpFieldPreEncoder'
   }
   manifest {
        attributes(
            "Main-Class": javaMainClass
        )
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task copyFatJar2jars_ready2use(type: Copy, dependsOn: 'shadowJar') {
    group 'distribution'
    description 'Copy the fatJar to bin/jars_ready2use/'
    from file("$buildDir/libs/mapsforgesrv-fatjar.jar")
    into file("$buildDir/bin/jars_ready2use/")
}

gradleLint.rules = ['all-dependency']