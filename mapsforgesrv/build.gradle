plugins {
    id 'java-library'
    id 'application'
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    id 'nebula.lint' version "18.0.3"
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src/main'
        }
    }
    test {
        java {
            srcDirs 'src/test'
        }
    }
 }

dependencies {
    def mapsforgeVersion = 'master-SNAPSHOT'
    def jettyVersion = '11.0.14'
    def slf4j = '+'

    // api "org.apache.commons:commons-math3:+"
    testImplementation "junit:junit:+"

    implementation "org.mapsforge:mapsforge-core:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-reader:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-themes:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-awt:${mapsforgeVersion}"
    // implementation "org.mapsforge:mapsforge-poi-awt:${mapsforgeVersion}"
    // implementation "org.mapsforge:mapsforge-poi:${mapsforgeVersion}"

    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty.http2:http2-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-util:${jettyVersion}"
    implementation "org.eclipse.jetty.toolchain:jetty-jakarta-servlet-api:+"

    runtimeOnly "org.slf4j:slf4j-reload4j:${slf4j}"
    implementation "org.slf4j:jul-to-slf4j:${slf4j}"
    implementation "org.slf4j:slf4j-api:${slf4j}"
    implementation "commons-cli:commons-cli:+"
    implementation "org.apache.commons:commons-lang3:+"
    runtimeOnly "org.xerial:sqlite-jdbc:+"
    runtimeOnly "net.sf.kxml:kxml2:+" 
    // implementation "com.formdev:svgSalamander:+"

    // implementation "com.google.jimfs:jimfs:+"
    implementation "com.github.marschall:memoryfilesystem:+"
}

mainClassName = "com.telemaxx.mapsforgesrv.MapsforgeSrv"

// https://github.com/johnrengelman/shadow
shadowJar {
   archiveBaseName.set('mapsforgesrv-fatjar')
   // https://github.com/johnrengelman/shadow/issues/341#issuecomment-445556737
   mergeServiceFiles {
        path = 'META-INF/services'
        include 'org.eclipse.jetty.http.HttpFieldPreEncoder'
   }
}

task copyFatJar2jars_ready2use(type: Copy, dependsOn: 'shadowJar') {
	group 'distribution'
	description 'Copy the fatJar to bin/jars_ready2use/'
	from file("$buildDir/libs/mapsforgesrv-fatjar.jar")
	into file("$buildDir/../bin/jars_ready2use/")
}

gradleLint.rules = ['all-dependency']