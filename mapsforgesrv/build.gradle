plugins {
    id 'java-library'
    id 'application'
    id 'com.github.johnrengelman.shadow' version "7.1.2"
}

repositories {
    mavenCentral()
}

dependencies {
    def mapsforgeVersion = '0.18.0'
    def jettyVersion = '11.0.9'
    def slf4j = '2.0.0-alpha7'

    api "org.apache.commons:commons-math3:3.6.1"
    testImplementation "junit:junit:4.13.2"
    
    implementation "org.mapsforge:mapsforge-core:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-reader:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-themes:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-map-awt:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-poi-awt:${mapsforgeVersion}"
    implementation "org.mapsforge:mapsforge-poi:${mapsforgeVersion}"

    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty.http2:http2-server:${jettyVersion}"

    implementation "org.slf4j:slf4j-log4j12:${slf4j}"
    implementation "org.slf4j:jul-to-slf4j:${slf4j}"
    implementation "commons-cli:commons-cli:1.5.0"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.xerial:sqlite-jdbc:3.36.0.3"
    implementation "net.sf.kxml:kxml2:2.3.0" 
    implementation "com.formdev:svgSalamander:1.1.3"
    implementation "com.github.marschall:memoryfilesystem:2.3.0"
}

mainClassName = "com.telemaxx.mapsforgesrv.MapsforgeSrv"

// https://github.com/johnrengelman/shadow
shadowJar {
   archiveBaseName.set('mapsforgesrv-fatjar')
   archiveClassifier.set('')
   archiveVersion.set('')
   // https://github.com/johnrengelman/shadow/issues/341#issuecomment-445556737
   mergeServiceFiles {
        path = 'META-INF/services'
        include 'org.eclipse.jetty.http.HttpFieldPreEncoder'
   }
}

task copyFatJar2jars_ready2use(type: Copy, dependsOn: 'shadowJar') {
	group 'distribution'
	description 'Copy the fatJar to bin/jars_ready2use/'
	from file("$buildDir/libs/mapsforgesrv-fatjar.jar")
	into file("$buildDir/../bin/jars_ready2use/")
}